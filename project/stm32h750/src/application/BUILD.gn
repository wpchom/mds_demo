pkggroup("mds") {
  deps = [
    "component/:button",
    "component/:fs",
    "component/:fs_emfs",
    "component/:lpc",
    "component/:systemview",
    "driver/chip/stm32h7xx/:chip_stm32h7xx_driver",
  ]
}

target_flags = [
  "-mcpu=cortex-m7",
  "-mthumb",
  "-mfloat-abi=hard",
  "-mfpu=fpv5-sp-d16",
]

config("project_config") {
  asmflags = target_flags + [
               "-fmessage-length=0",
               "-fdiagnostics-color",
               "-fno-common",
               "-x",
               "assembler-with-cpp",
             ]
  cflags = target_flags + [
             "-fmessage-length=0",
             "-fdiagnostics-color",
             "-fno-common",
             "-ffreestanding",
             "-fsigned-char",
             "-ffunction-sections",
             "-fdata-sections",
             "-fanalyzer",

             "-Wall",
             "-Wextra",
             "-Wnull-dereference",
             "-Wstringop-overflow",
             "-Wdouble-promotion",
             "-Wshadow",
             "-Wlogical-op",
             "-Wredundant-decls",

             "-g",
             "-Os",
           ]
  ldflags = target_flags + [
              "-nostartfiles",
              "--specs=nano.specs",
              "--specs=nosys.specs",
              "-Wl,--gc-sections",
              "-Wl,--print-memory-usage",
              "-Wl,--no-warn-rwx-segments",
            ]
}

executable("${project_name}") {
  sources = [ "app_main.c" ]

  deps = [ ":mds" ]

  inputs = [ rebase_path("STM32H750XB_FLASH.ld") ]

  ldflags = [
    "-T" + rebase_path("STM32H750XB_FLASH.ld"),
    "-Wl,-Map=" + rebase_path("${root_out_dir}/${target_name}.map"),
  ]
}

arm_none_eabi_action("${project_name}.dis") {
  action = "arm-none-eabi-objdump"
  args = [
    "-SFhlx",
    rebase_path("${root_out_dir}/${project_name}.elf") + " > " +
        rebase_path("${root_out_dir}/${target_name}"),
  ]
  deps = [ ":${project_name}" ]
  outputs = [ "${root_out_dir}/${target_name}" ]
}

arm_none_eabi_action("${project_name}.bin") {
  action = "arm-none-eabi-objcopy"
  args = [
    "-Obinary",
    "-R",
    ".logstr",
    rebase_path("${root_out_dir}/${project_name}.elf"),
    rebase_path("${root_out_dir}/${target_name}"),
  ]
  deps = [ ":${project_name}" ]
  outputs = [ "${root_out_dir}/${target_name}" ]
}

arm_none_eabi_action("${project_name}.hex") {
  action = "arm-none-eabi-objcopy"
  args = [
    "-Oihex",
    "-R",
    ".logstr",
    rebase_path("${root_out_dir}/${project_name}.elf"),
    rebase_path("${root_out_dir}/${target_name}"),
  ]
  deps = [ ":${project_name}" ]
  outputs = [ "${root_out_dir}/${target_name}" ]
}

arm_none_eabi_action("${project_name}.logstr") {
  action = "arm-none-eabi-objcopy"
  args = [
    "-Obinary",
    "-j",
    ".logstr",
    rebase_path("${root_out_dir}/${project_name}.elf"),
    rebase_path("${root_out_dir}/${target_name}"),
  ]
  deps = [ ":${project_name}" ]
  outputs = [ "${root_out_dir}/${target_name}" ]
}

group("default") {
  deps = [
    ":${project_name}",
    ":${project_name}.bin",
    ":${project_name}.dis",
    ":${project_name}.hex",
    ":${project_name}.logstr",
  ]
}
